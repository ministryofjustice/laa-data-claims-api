plugins {
    id 'io.freefair.lombok' version '8.13.1'
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'
    id "com.dorongold.task-tree" version "2.1.1"
    id("com.diffplug.spotless") version "7.2.1"
    id "io.sentry.jvm.gradle" version "5.12.2"
    id 'au.com.dius.pact' version '4.6.19'
}

def versions = [
    sentry : '8.28.0',
    testcontainers: '1.20.1'
]

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/ClaimsDataApplication.class'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/ClaimsDataApplication.class'])
        }))
    }
}

// Dependency overrides
// example:
// Upgrade to fix CVE-xxxx-xxxxx
// ext['dependency-name.version'] = 'x.x.x'
ext['commons-lang3.version'] = '3.18.0'

dependencyManagement {
    dependencies {
        dependencySet(group: 'io.sentry', version: versions.sentry) {
            entry 'sentry-spring-boot-starter'
            entry 'sentry-logback'
        }
    }
}

dependencies {
    implementation project(':claims-data:api')

    //Enable access token authentication
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-auth'
    //Common exception handling
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-application-exception'
    //If the ScanForSql annotation is used, the values will be checked and a warning logged if suspicious SQL-like commands are found in any values
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-sql-scanner'
    implementation 'org.springframework.boot:spring-boot-starter-aspectj'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'

    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    implementation 'org.javers:javers-spring-boot-starter-sql:7.10.0'
    implementation platform('com.fasterxml.jackson:jackson-bom:2.19.2')
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'
    implementation platform("software.amazon.awssdk:bom:2.32.22")
    implementation "software.amazon.awssdk:sqs"
    implementation 'software.amazon.awssdk:sts'
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.1.0'

    // Sentry
    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation "io.sentry:sentry-logback:$versions.sentry"
    implementation "io.sentry:sentry-spring-boot-4:$versions.sentry"

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    runtimeOnly "org.flywaydb:flyway-database-postgresql"
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
    testImplementation "org.testcontainers:junit-jupiter:$versions.testcontainers"
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    // Add this to ensure Spring Security classes are available during tests
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.testcontainers:postgresql:$versions.testcontainers"
    testImplementation "org.testcontainers:localstack:$versions.testcontainers"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly "org.flywaydb:flyway-database-postgresql"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Source: https://mvnrepository.com/artifact/au.com.dius.pact.provider/spring7
    implementation 'au.com.dius.pact.provider:spring7:4.7.0-beta.3'
    // If the above doesn't resolve it, add it specifically for Pact
    testImplementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.boot:spring-boot-autoconfigure'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.13'
    testRuntimeOnly "org.flywaydb:flyway-database-postgresql"
}

sourceSets {
    pactTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    pactTestImplementation.extendsFrom testImplementation
    pactTestRuntimeOnly.extendsFrom testRuntimeOnly
    
    // Explicitly associate the configuration with the source set's classpath
    // This is often what's missing in CI environments
    pactTestCompileClasspath.extendsFrom testCompileClasspath
    pactTestRuntimeClasspath.extendsFrom testRuntimeClasspath
}

// Ensure the pactTest source set uses the configurations we just set up
sourceSets.pactTest.compileClasspath += configurations.pactTestCompileClasspath
sourceSets.pactTest.runtimeClasspath += configurations.pactTestRuntimeClasspath

tasks.register('pactTest', Test) {
    description = 'Runs the pact tests.'
    group = 'verification'
    useJUnitPlatform()

    testClassesDirs = sourceSets.pactTest.output.classesDirs
    classpath = sourceSets.pactTest.runtimeClasspath

    // Pass environment variables into the test process as System Properties
    systemProperty 'pact.verifier.publishResults', System.getenv('PACT_VERIFIER_PUBLISH_RESULTS') ?: 'true'
    systemProperty 'pact.provider.version', System.getenv('CI_COMMIT_SHA') ?: '1.0.0'
    systemProperty 'pactbroker.host', System.getenv('PACT_BROKER_URL') ?: 'http://localhost:9292'
    systemProperty 'pactbroker.auth.username', System.getenv('PACT_BROKER_USERNAME') ?: 'user'
    systemProperty 'pactbroker.auth.password', System.getenv('PACT_BROKER_PASSWORD') ?: 'pass'

    // Resolve the directory path to a String for the system property
    systemProperty 'pact.rootDir', layout.buildDirectory.dir("pacts").get().asFile.path
}

tasks.named('processPactTestResources') {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}


spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat()
    }
}

tasks.register("checkstyleStaged", Checkstyle) {
    group = "verification"
    description = "Runs Checkstyle on staged files only"

    def filesToCheck = project.findProperty("checkstyleFiles")
    if (filesToCheck != null) {
        source = files(filesToCheck.split(","))
    } else {
        source = sourceSets.main.allJava
    }

    classpath = sourceSets.main.compileClasspath
}
