plugins {
    id 'io.freefair.lombok' version '8.13.1'
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'
    id "com.dorongold.task-tree" version "2.1.1"
    id("com.diffplug.spotless") version "7.2.1"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/ClaimsDataApplication.class'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/ClaimsDataApplication.class'])
        }))
    }
}

//Overriding tomcat and commons-lang3 versions coming from laa-ccms-spring-boot-gradle-plugin -> spring boot 3.5.3
// to avoid snyk warning that stops the build
//Remove the line below when the laa-ccms-spring-boot-gradle-plugin is upgraded
ext['tomcat.version'] = '10.1.45'
// Upgrade to fix CVE-2025-58056
ext['netty.version'] = '4.1.125.Final'
ext['commons-lang3.version'] = '3.18.0'

dependencies {
    implementation project(':claims-data:api')

    //Enable access token authentication
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-auth'
    //Common exception handling
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-application-exception'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.sentry:sentry-spring-boot-starter-jakarta:7.18.1'
    implementation 'io.sentry:sentry-logback:7.18.1'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    implementation 'org.projectlombok:lombok:1.18.38'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    implementation 'org.javers:javers-spring-boot-starter-sql:7.6.2'
    implementation platform('com.fasterxml.jackson:jackson-bom:2.19.2')
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.10.3'
    implementation platform("software.amazon.awssdk:bom:2.32.22")
    implementation "software.amazon.awssdk:sqs"
    implementation 'software.amazon.awssdk:sts'
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.1.0'

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    runtimeOnly "org.flywaydb:flyway-database-postgresql"
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation "org.testcontainers:localstack:1.20.1"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

}

spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat()
    }
}

tasks.register("checkstyleStaged", Checkstyle) {
    group = "verification"
    description = "Runs Checkstyle on staged files only"

    def filesToCheck = project.findProperty("checkstyleFiles")
    if (filesToCheck != null) {
        source = files(filesToCheck.split(","))
    } else {
        source = sourceSets.main.allJava
    }

    classpath = sourceSets.main.compileClasspath
}
