plugins {
    id 'io.freefair.lombok' version '8.13.1'
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'
    id "com.dorongold.task-tree" version "2.1.1"
    id("com.diffplug.spotless") version "7.2.1"
    id "io.sentry.jvm.gradle" version "5.12.2"
    id 'au.com.dius.pact' version '4.6.19'
}

def versions = [
        sentry        : '8.28.0',
        testcontainers: '1.20.1'
]

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/ClaimsDataApplication.class'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/ClaimsDataApplication.class'])
        }))
    }
}

// Dependency overrides
// example:
// Upgrade to fix CVE-xxxx-xxxxx
// ext['dependency-name.version'] = 'x.x.x'
ext['commons-lang3.version'] = '3.18.0'

dependencyManagement {
    dependencies {
        dependencySet(group: 'io.sentry', version: versions.sentry) {
            entry 'sentry-spring-boot-starter'
            entry 'sentry-logback'
        }
    }
}

dependencies {
    // Upgrade to fix CVE-2026-1225 vulnerability in logback-core
    implementation 'ch.qos.logback:logback-core:1.5.25'
    implementation 'ch.qos.logback:logback-classic:1.5.25'

    implementation project(':claims-data:api')

    //Enable access token authentication
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-auth'
    //Common exception handling
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-application-exception'
    //If the ScanForSql annotation is used, the values will be checked and a warning logged if suspicious SQL-like commands are found in any values
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-sql-scanner'
    implementation 'org.springframework.boot:spring-boot-starter-aspectj'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'

    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    implementation 'org.javers:javers-spring-boot-starter-sql:7.10.0'
    implementation platform('com.fasterxml.jackson:jackson-bom:2.19.2')
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'
    implementation platform("software.amazon.awssdk:bom:2.32.22")
    implementation "software.amazon.awssdk:sqs"
    implementation 'software.amazon.awssdk:sts'
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.1.0'

    // Sentry
    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation "io.sentry:sentry-logback:$versions.sentry"
    implementation "io.sentry:sentry-spring-boot-4:$versions.sentry"

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    runtimeOnly "org.flywaydb:flyway-database-postgresql"
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
    testImplementation "org.testcontainers:junit-jupiter:$versions.testcontainers"
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    // Add this to ensure Spring Security classes are available during tests
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.testcontainers:postgresql:$versions.testcontainers"
    testImplementation "org.testcontainers:localstack:$versions.testcontainers"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly "org.flywaydb:flyway-database-postgresql"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testRuntimeOnly "org.flywaydb:flyway-database-postgresql"

    // Upgrade to fix CVE-2026-24734 vulnerability in apache tomcat
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:11.0.18'
    implementation 'org.apache.tomcat.embed:tomcat-embed-el:11.0.18'
    implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:11.0.18' 
}

sourceSets {
    pactTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    pactTestImplementation.extendsFrom(testImplementation)
    pactTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}


dependencies {
    // Source: https://mvnrepository.com/artifact/au.com.dius.pact.provider/spring7
    pactTestImplementation 'au.com.dius.pact.provider:spring7:4.7.0-beta.3'
    pactTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    pactTestImplementation 'org.springframework.boot:spring-boot-autoconfigure'
    pactTestImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    pactTestImplementation 'org.apache.httpcomponents:httpclient:4.5.13'
}

tasks.register('pactTest', Test) {
    description = 'Runs the pact tests.'
    group = 'verification'
    useJUnitPlatform()

    testClassesDirs = sourceSets.pactTest.output.classesDirs
    classpath = sourceSets.pactTest.runtimeClasspath

    // Publish if both username and password environment variables are provided
    def canPublish =
            providers.environmentVariable('PACT_BROKER_USERNAME').isPresent() &&
            providers.environmentVariable('PACT_BROKER_PASSWORD').isPresent()

    if (canPublish) {
        systemProperty 'pact.verifier.publishResults', 'true'
    }

    // Pass environment variables into the test process as System Properties
    systemProperty 'pact.provider.version', providers.environmentVariable('CI_COMMIT_SHA').getOrElse('1.0.0')
    systemProperty 'pactbroker.url', providers.environmentVariable('PACT_BROKER_URL').getOrElse('https://laa-data-pact-broker.apps.live.cloud-platform.service.justice.gov.uk/')
    systemProperty 'pactbroker.auth.username', providers.environmentVariable('PACT_BROKER_USERNAME').getOrElse('ABC')
    systemProperty 'pactbroker.auth.password', providers.environmentVariable('PACT_BROKER_PASSWORD').getOrElse('ABC')

    // Only set the consumer filter and branch selectors if PACT_CONSUMER_NAME is provided
    def consumerName = providers.environmentVariable('PACT_CONSUMER_NAME').orNull

    if (consumerName) {
        systemProperty 'pact.filter.consumers', consumerName
    }
    def branch = providers.environmentVariable('PACT_CONSUMER_BRANCH').getOrElse('main')
    systemProperty 'pactbroker.consumerversionselectors.rawjson', "[{\"branch\":\"${branch}\"}]"

    // Resolve the directory path to a String for the system property
    systemProperty 'pact.rootDir', layout.buildDirectory.dir("pacts").get().asFile.path
}

tasks.named('processPactTestResources') {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat()
    }
}

tasks.register("checkstyleStaged", Checkstyle) {
    group = "verification"
    description = "Runs Checkstyle on staged files only"

    def filesToCheck = project.findProperty("checkstyleFiles")
    if (filesToCheck != null) {
        source = files(filesToCheck.split(","))
    } else {
        source = sourceSets.main.allJava
    }

    classpath = sourceSets.main.compileClasspath
}
